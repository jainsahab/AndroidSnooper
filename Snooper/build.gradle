apply plugin: 'com.android.library'
apply plugin: 'realm-android'
apply plugin: 'maven-publish'
apply plugin: 'spoon'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bundleRelease
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.2"
    group "com.github.jainsahab"
    version snooperVersion

    dataBinding {
        enabled = true
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName snooperVersion
        testInstrumentationRunner "com.prateekj.snooper.SnooperInstrumentationRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTestDataHelpers/java'
        String sharedTestAssetDir = 'src/sharedTestDataHelpers/assets'
        test {
            java.srcDir sharedTestDir
            assets.srcDir sharedTestAssetDir
        }
        androidTest {
            java.srcDir sharedTestDir
            assets.srcDir sharedTestAssetDir
        }
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }
}

spoon {
    debug = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile "com.android.support:recyclerview-v7:24.2.1"
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'com.google.collections:google-collections:1.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.android.support:support-annotations:24.0.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'io.reactivex:rxjava:1.1.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.5.4'
}

apply from: '../maven-push.gradle'